apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt' //annotation processor that works with kotlin as well as java


def keystorePropsFile = rootProject.file('keystore.properties')



android {
    if (keystorePropsFile.exists()) {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(keystorePropsFile))
        signingConfigs {
            release {
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
            }
        }
    }

    compileSdkVersion 29
    defaultConfig {
        applicationId "com.minirogue.starwarscanontracker"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 22
        versionName "1.2.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

ext {
    daggerVersion = '2.16'
    androidXTestVersion = '1.2.0'
    jUnitVersion = '4.12'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //Fastscroll: https://github.com/zhanghai/AndroidFastScroll
    implementation 'me.zhanghai.android.fastscroll:library:1.1.1'

    //Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "junit:junit:$jUnitVersion"
    testImplementation "junit:junit:$jUnitVersion"
    androidTestImplementation "androidx.test:core:$androidXTestVersion"
    androidTestImplementation "androidx.test:rules:$androidXTestVersion"
    androidTestImplementation "androidx.test:runner:$androidXTestVersion"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'



    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    //lifecycle components
    //implementation 'androidx.fragment:fragment-ktx:1.1.0' //for lazy viewmodel instantiation
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    //Dagger2(dependency injection)
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

    //Room (databases)
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'

    //Fresco (image loading)
    implementation 'com.facebook.fresco:fresco:1.13.0'

}
