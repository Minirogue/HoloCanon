apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'


def keystorePropsFile = rootProject.file('keystore.properties')
def versionPropertiesFile = rootProject.file("version.properties")

task bumpVersionCode {
    doLast {
        def versionProperties = new Properties()
        versionProperties.load(new FileInputStream(versionPropertiesFile))

        def code = versionProperties['VERSION_CODE'].toInteger() + 1

        versionProperties['VERSION_CODE'] = code.toString()
        versionProperties.store(versionPropertiesFile.newWriter(), null)
    }
}

android {
    if (keystorePropsFile.exists()) {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(keystorePropsFile))
        signingConfigs {
            release {
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
            }
        }
    }
    def versionProperties = new Properties()
    if (versionPropertiesFile.exists()) {
        versionProperties.load(new FileInputStream(versionPropertiesFile))
    }

    def date = new Date()
    def verName = date.format('yy.M.d')

    compileSdkVersion 31
    defaultConfig {
        applicationId "com.minirogue.starwarscanontracker"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode versionProperties['VERSION_CODE'].toInteger()
        versionName verName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
//            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Project Modules
    implementation project(":core")
    implementation project(":usecase")

    // Android
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:2.1.2"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // Fastscroll: https://github.com/zhanghai/AndroidFastScroll
    implementation "me.zhanghai.android.fastscroll:library:1.1.7"

    // Testing
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "junit:junit:$jUnitVersion"
    testImplementation "junit:junit:$jUnitVersion"
    androidTestImplementation "androidx.test:core:$androidXTestVersion"
    androidTestImplementation "androidx.test:rules:$androidXTestVersion"
    androidTestImplementation "androidx.test:runner:$androidXTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // lifecycle components
    implementation "androidx.fragment:fragment-ktx:1.4.0" //for lazy viewmodel instantiation
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion" // TODO deprecate
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // dependency injection
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$jetpackHiltVersion"
    kapt "androidx.hilt:hilt-compiler:$jetpackHiltVersion"

    // Image loading (image loading)
    implementation "io.coil-kt:coil:$coilVersion"
}
